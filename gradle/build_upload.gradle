apply plugin: 'maven'
apply plugin: 'signing'

signing {
    required { false }
    sign configurations.archives
}

group = "cn.jzvd"
archivesBaseName = "jiaozivideoplayer"
version = "7.6.0"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//            repository(url: uri('build/buildUpload'))
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'jiaozivideoplayer'
                packaging 'aar'
                // optionally artifactId can be defined here
                description 'android videoplayer videoview 安卓视频播放器 全屏 播放器列表全屏 仿今日头条UI'
                url 'https://github.com/Jzvd/JiaoZiVideoPlayer'

                scm {
                    connection 'https://github.com/Jzvd/JiaoZiVideoPlayer'
                    developerConnection 'https://github.com/Jzvd/JiaoZiVideoPlayer'
                    url 'https://github.com/Jzvd/JiaoZiVideoPlayer'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'http://mit-license.org/'
                    }
                }

                developers {
                    developer {
                        id 'Nathen'
                        name 'Nathen'
                        email '1066666651@qq.com'
                    }
                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    options {
        encoding = "UTF-8"
    }
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

